plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'com.google.protobuf' version '0.9.4'
}

group = 'com.rtr'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/suzzingV/Rule-the-Rock-BE")
        credentials {
            username = System.getenv("GITHUB_USERNAME")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

bootJar { enabled = true }
jar { enabled = false }

dependencies {
    // Common module
    implementation 'com.rtr:common-module:0.0.1-SNAPSHOT'
    //SpringBoot DATA JPA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    //SpringBoot Web
    implementation 'org.springframework.boot:spring-boot-starter-web'
    //Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    //SpringBoot Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.batch:spring-batch-test'
    //SpringBoot Security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    //SpringBoot Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    //JWT
    implementation 'com.auth0:java-jwt:4.2.1'
    //swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.4'
    // MYSQL
    runtimeOnly 'com.mysql:mysql-connector-j'
    //REDIS
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    //H2
    runtimeOnly 'com.h2database:h2'
    // grpc
    implementation 'io.grpc:grpc-netty-shaded:1.60.0'
    implementation 'io.grpc:grpc-protobuf:1.60.0'
    implementation 'io.grpc:grpc-stub:1.60.0'
    implementation 'io.grpc:grpc-core:1.60.0'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // Java 8 필요
    implementation 'net.devh:grpc-server-spring-boot-starter:2.15.0.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
}

tasks.register('printEnv') {
    doLast {
        println "GITHUB_USERNAME: ${System.getenv('GITHUB_USERNAME')}"
        println "GITHUB_TOKEN: ${System.getenv('GITHUB_TOKEN')}"
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.1"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.60.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {}
            }
            task.plugins {
                grpc {}
            }
        }
    }
}

sourceSets {
    main {
        proto {
            srcDir '../proto'
        }
        java {
            // 이 경로를 Java 소스 경로로 포함시킴!
            srcDirs += 'build/generated/source/proto/main/java'
            srcDirs += 'build/generated/source/proto/main/grpc'
        }
    }
}
